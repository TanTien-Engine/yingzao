import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.blueprint" for Blueprint
import "blueprint.variant" for Variant, VAR_TYPE_ARRAY
import "editor.node_cache" for NODE_CACHE
import "archgraph.variant" for PIN_TYPE_HORI_GONG, PIN_TYPE_FANG
import "archgraph.geo_helper" for GeoHelper
import "archgraph.node_helper" for NodeHelper
import "archgraph.constant" for CAI_HEIGHT, ZU_CAI_HEIGHT
import "archgraph.component" for Component
import "archgraph.nodes.dou" for SAN_DOU, QIXIN_DOU
import "geograph.nodes.brush_get_size" for BrushGetSize
import "maths.vector" for Vector3

var GUAZI_GONG = NODE_CACHE.fetch("C:/projects/yingzao/00_dougong2/32_guazi_gong.ves", nil)
var MAN_GONG   = NODE_CACHE.fetch("C:/projects/yingzao/00_dougong2/33_man_gong.ves", nil)
var LING_GONG  = NODE_CACHE.fetch("C:/projects/yingzao/00_dougong2/34_ling_gong.ves", nil)

var SUN_GD = NODE_CACHE.fetch("C:/projects/yingzao/00_dougong2/60_sun_gd.ves", nil)

var JIAOHU_CUT = NODE_CACHE.fetch("C:/projects/yingzao/00_dougong2/20_jiaohu_tu_cut_body.ves", nil)
var QIXIN_CUT  = NODE_CACHE.fetch("C:/projects/yingzao/00_dougong2/20_qixin_cut_body.ves", nil)

class HoriGong is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "prev", PIN_TYPE_HORI_GONG),
		]
		this.exports = [
			Pin(this, "next", PIN_TYPE_HORI_GONG),
		]

		this.layout()

		this.comp = Component()
	}

	calc_value(idx)
	{
		var pos = NodeHelper.calc_curr_pos(this, 0)
		return Variant(pos.add(Vector3(0, ZU_CAI_HEIGHT, 0)))
	}

	resolve(dg)
	{
		// select gong
		var gong = this.select_gong()
		if (gong == MAN_GONG) {
			this.exports[0].type = PIN_TYPE_FANG
		}

		// translate
		var pos = NodeHelper.calc_curr_pos(this, 0)
		var v_geo = GeoHelper.translate_geo(gong, pos)

		// conn prev
//		v_geo = this.add_joint(v_geo, pos)

		// conn next
		v_geo = this.add_dou(dg, v_geo, pos)

		this.comp.pos = pos
		this.comp.v_geo = v_geo
	}

	select_gong()
	{
		var prev = Blueprint.get_input_node(this, 0)
		if (prev and prev is HoriGong) {
			return MAN_GONG
		} else {
			return GUAZI_GONG
		}
	}

	add_joint(v_geo, pos)
	{
		var cut_node = nil

		var prev = Blueprint.get_input_node(this, 0)
		if (prev and prev is HoriGong) {
			cut_node = QIXIN_CUT
		} else {
			cut_node = JIAOHU_CUT
		}

		return GeoHelper.joint_cut_geo(v_geo, cut_node, pos)
	}

	add_dou(dg, v_geo, pos)
	{
		var next = Blueprint.get_output_node(this, 0)
		if (!next) {
			return v_geo
		}

		var aabb = []
		BrushGetSize.calc_aabb(v_geo, aabb)
		var x_min = aabb[0].x
		var x_max = aabb[1].x

		var dist = 0.05
		var l_pos = Vector3(x_min + dist, pos.y + CAI_HEIGHT, pos.z)
		var r_pos = Vector3(x_max - dist, pos.y + CAI_HEIGHT, pos.z)

		var l_sun = GeoHelper.translate_geo(SUN_GD, l_pos)
		var r_sun = GeoHelper.translate_geo(SUN_GD, r_pos)

		var l_dou = GeoHelper.translate_geo(SAN_DOU, l_pos)
		var r_dou = GeoHelper.translate_geo(SAN_DOU, r_pos)

		// cut sun
		l_dou = GeoHelper.joint_cut_geo(l_dou, SUN_GD, l_pos)
		r_dou = GeoHelper.joint_cut_geo(r_dou, SUN_GD, r_pos)

		var has_c = !dg.has_vert_gong(this.comp.y_idx)
		if (has_c)
		{
			var c_pos = pos.add(Vector3(0, CAI_HEIGHT, 0))

			var c_sun = GeoHelper.translate_geo(SUN_GD, c_pos)
			var c_dou = GeoHelper.translate_geo(QIXIN_DOU, c_pos)

			c_dou = GeoHelper.joint_cut_geo(c_dou, SUN_GD, c_pos)

			v_geo = GeoHelper.joint_cut_geo(v_geo, SUN_GD, [ l_pos, c_pos, r_pos ])

			return Variant([ v_geo, [ l_sun, c_sun, r_sun ], [ l_dou, c_dou, r_dou ] ])
		}
		else
		{
			v_geo = GeoHelper.joint_cut_geo(v_geo, SUN_GD, [ l_pos, r_pos ])

			return Variant([ v_geo, [ l_sun, r_sun ], [ l_dou, r_dou ] ])
		}
	}

	get_title()
	{
		var gong = this.select_gong()
		if (gong == MAN_GONG) {
			return "慢栱"
		} else {
			return "瓜子栱"
		}
	}
}