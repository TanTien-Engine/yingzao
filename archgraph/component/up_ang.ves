import "blueprint.blueprint" for Blueprint
import "blueprint.variant" for Variant
import "blueprint.variant_proxy" for VariantProxy
import "geograph.nodes.knife" for Knife
import "scenegraph.nodes.transform3d" for Transform3d
import "archgraph.component.vert_gong" for VertGong
import "archgraph.trans_nodes" for TransNodes
import "archgraph.context" for CONTEXT
import "archgraph.node_helper" for NodeHelper
import "archgraph.constant" for CAI_HEIGHT, ZU_CAI_HEIGHT, QI_HEIGHT
import "archgraph.joints_nodes" for JointsNodes
import "archgraph.tools_nodes" for ToolsNodes
import "maths.vector" for Vector3
import "editor.node_cache" for NODE_CACHE
import "math" for Math

var ANG    = NODE_CACHE.fetch("../yingzao/00_dougong2/38_up_ang.ves", nil)
var XUE    = NODE_CACHE.fetch("../yingzao/00_dougong2/38_xue_qi.ves", nil)
var GD_SUN = NODE_CACHE.fetch("../yingzao/00_dougong2/10_gd_sun.ves", nil)

class UpAng is VertGong
{
	calc_value(idx)
	{
		if (idx == 2) 
		{
			var ang_pos = this.calc_ang_pos()
			ang_pos.y = ang_pos.y + this.calc_first_dy()
			return Variant(ang_pos)
		} 
		else 
		{
			return super.calc_value(idx)
		}
	}

	resolve(dg)
	{
		var prev = Blueprint.get_input_node(this, "prev")
		if (prev) 
		{
			this.inner_hop = prev.inner_hop + 1
			if (prev is UpAng) {
				this.outer_hop = prev.outer_hop
			} else {
				this.outer_hop = prev.outer_hop + 1
			}
		} 
		else 
		{
			this.outer_hop = 1
			this.inner_hop = 1		
		}

		if (CONTEXT.s_up_ang_angle) {
			Blueprint.connect(CONTEXT.s_up_ang_angle, "up_ang_slope", ANG, "slope")
		}

		var ang_pos = this.calc_ang_pos()
		ang_pos.y = ang_pos.y + this.calc_first_dy()

		var v_geo = TransNodes.translate(ANG, ang_pos)
		if (prev is UpAng)
		{
			var pos_n = NodeHelper.calc_curr_pos(this, "prev")
			import "archgraph.component.shua_tou" for ShuaTou
			var v_shua = ShuaTou.create_geo(this.outer_hop, this.inner_hop, pos_n)

			// ang cut
			v_shua = this.ang_plane_cut(v_shua)

			// conn next
			v_shua = this.add_dou(v_shua, true, true)

//			v_geo = ToolsNodes.brush_merge([v_xue, v_geo])
			var geo_list = [ v_geo ]
			for (var i in v_shua.value) {
				geo_list.add(i)
			}
			v_geo = Variant(geo_list)
		}
		else
		{
			Blueprint.connect(CONTEXT.s_up_ang_angle, "up_ang_slope", XUE, "slope")
			var v_xue = TransNodes.translate(XUE, ang_pos)

			var pos_n = NodeHelper.calc_curr_pos(this, "prev")
			import "archgraph.component.hua_gong" for HuaGong
			var v_hua = HuaGong.create_geo(this.outer_hop, this.inner_hop, pos_n)

			// ang cut
			v_hua = this.ang_plane_cut(v_hua)

			// conn next
			v_hua = this.add_dou(v_hua, true, true)

//			v_geo = ToolsNodes.brush_merge([v_xue, v_geo])
			var geo_list = [ v_geo ]
			for (var i in v_hua.value) {
				geo_list.add(i)
			}
			v_geo = Variant(geo_list)
		}

		this.comp.pos = ang_pos
		this.comp.v_geo = v_geo
		this.comp.v_geo_c = this.comp.v_geo
	}

	get_title()
	{
		return "上昂"
	}

	calc_ang_pos()
	{
		var dist = 0
		if (CONTEXT.gen_corner) {
			dist = CONTEXT.step_len_c_func(-this.inner_hop, true)
		} else {
			dist = CONTEXT.step_len_func(-this.inner_hop, true)
		}

		var prev = Blueprint.get_input_node(this, "prev")
		if (prev is UpAng)
		{
			var pos = prev.calc_ang_pos()
			if (pos) {
				return pos.add(Vector3(0, ZU_CAI_HEIGHT, -dist))
			}
		}
		else
		{
			var pos = Blueprint.calc_output_value(prev, "inner").value
			if (pos) {
				return pos.add(Vector3(0, ZU_CAI_HEIGHT * 2, -dist))
			}
		}

		return Vector3(0, 0, 0)
	}

	calc_first_dy()
	{
		var next = NodeHelper.get_output_valid_node(this, "next")
		if (next is UpAng and CONTEXT.s_up_ang_angle)
		{
			var slope = Blueprint.calc_output_value(CONTEXT.s_up_ang_angle, "up_ang_slope").value
			var angle = Math.atan2(slope, 1)
			return ZU_CAI_HEIGHT - (CONTEXT.step_len_func(-this.inner_hop - 1, true) * slope + CAI_HEIGHT / Math.cos(angle))
		}
		return 0
	}

	ang_plane_cut(geo)
	{
		var ang_pos = this.calc_ang_pos()
		ang_pos.y = ang_pos.y + this.calc_first_dy()
		return UpAng.ang_plane_cut(geo, ang_pos)
	}

	static ang_plane_cut(geo, pos)
	{
		var KNIFE = Knife()
		KNIFE.query_param("keep").set_value("above")
		KNIFE.query_param("seam").set_value(true)

		var trans = Transform3d()
		Blueprint.connect(ANG, "plane", trans, "obj")

		var pos_node = VariantProxy(Variant(pos))
		Blueprint.connect(pos_node, "out", trans, "translate")

		var geo_node = VariantProxy(geo)
		Blueprint.connect(geo_node, "out", KNIFE, "geo")
		Blueprint.connect(trans, "obj", KNIFE, "plane")
		return Blueprint.calc_output_value(KNIFE, "geo")		
	}
}