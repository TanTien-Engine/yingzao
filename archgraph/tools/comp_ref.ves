import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.blueprint" for Blueprint
import "blueprint.variant" for Variant
import "blueprint.node_param" for NodeParam
import "archgraph.variant" for VarComp, VAR_TYPE_COMPONENT
import "archgraph.context" for CONTEXT

class CompRef is Node
{
	init()
	{
		super.init()

		this.exports = [
			Pin(this, "comp", VAR_TYPE_COMPONENT),
		]

		this.params.add(NodeParam("y", 0, { "integer" : true, "min" : 0, "max" : 10 }))
		this.params.add(NodeParam("x", 0, { "integer" : true, "min" : 0, "max" : 10 }))
		this.params.add(NodeParam("inner", false))

		this.layout()
	}

	calc_value(idx)
	{
		var node = this.get_node_ref()
		if (node) {
			return Variant(VAR_TYPE_COMPONENT, VarComp(node.comp))
		} else {
			return nil
		}
	}

	get_node_ref()
	{
		import "archgraph.tools.vert_split" for VertSplit

		if (!CONTEXT.dg) {
			return nil
		}

		var y = this.query_param("y").value
		var x = this.query_param("x").value

		if (y >= CONTEXT.dg.layers.count or x >= CONTEXT.dg.layers[y].count) {
			return nil
		}

		var node = CONTEXT.dg.layers[y][x]
		if (node is List) 
		{
			var list = node

			if (node.isEmpty) 
			{
				node = nil
			} 
			else if (list[0] is VertSplit) 
			{
				var inner = this.query_param("inner").value
				if (inner)
				{
					node = Blueprint.get_output_node(list[0], "inner")
					if (!node) {
						node = Blueprint.get_output_node(list[0], "outer")
					}
				}
				else
				{
					node = Blueprint.get_output_node(list[0], "outer")
					if (!node) {
						node = Blueprint.get_output_node(list[0], "inner")
					}
				}
			} 
			else 
			{
				node = list[0]
			}
		}

		return node	
	}
}