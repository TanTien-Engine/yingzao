import "archgraph.context" for CONTEXT
import "maths.vector" for Vector2, Vector3
import "math" for Math

class CoordTrans
{
	static calc_hori_gong_rotated_pos(pos, offset, angle)
	{
		var pos2 = Vector2(0, -(pos.x + offset)).rotated(-angle).add(Vector2(0, pos.z))
		return Vector3(pos2.x, pos.y, pos2.y)
	}

	static calc_pos(idx)
	{
		var pos = 0

		if (idx > 0) {
			for (var i in 1 .. idx + 1) {
				pos = pos + CONTEXT.step_len_func(i, true)
			}
		} else {
			for (var i in 1 .. -idx + 1) {
				pos = pos - CONTEXT.step_len_func(-i, true)
			}
		}

		return pos
	}

	static calc_step_len_section(start_idx, num)
	{
		var sum = 0

		var count = 0
		var curr = start_idx
		var n = Math.abs(num)
		while (count < n) 
		{
			if (curr != 0) {
				sum = sum + CONTEXT.step_len_func(curr)
				count = count + 1
			}
			if (num < 0) {
				curr = curr - 1
			} else {
				curr = curr + 1
			}
		}

		return sum
	}

	static calc_corner_pos(y_idx, x_idx)
	{
		var y = this.calc_pos(y_idx) * 1.414

		var len = CoordTrans.calc_step_len_section(y_idx, x_idx)
		var p2 = Vector2(0, 0)
		if (x_idx > 0) {
			p2.y = len
		} else {
			p2.y = -len
		}
		p2.rotate(Math.pi() / 4)

		return Vector2(0, y).add(p2)
	}
}