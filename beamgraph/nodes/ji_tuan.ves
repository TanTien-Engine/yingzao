import "blueprint.pin" for Pin
import "blueprint.node" for Node
import "blueprint.node_param" for NodeParam
import "blueprint.variant" for Variant
import "blueprint.variant_proxy" for VariantProxy
import "blueprint.blueprint" for Blueprint
import "gonggraph.component.lu_dou" for LU_DOU
import "beamgraph.variant" for PIN_TYPE_TUAN
import "beamgraph.comp_nodes" for CompNodes
import "beamgraph.gong_helper" for GongHelper
import "beamgraph.trans_nodes" for TransNodes
import "beamgraph.context" for CONTEXT
import "beamgraph.constant" for BEAM_DX, FRAME_DIST
import "beamgraph.tuan_helper" for TuanHelper
import "maths.vector" for Vector3
import "math" for Math
import "editor.node_cache" for NODE_CACHE

class JiTuan is Node
{
	init()
	{
		super.init()

		this.exports = [
			Pin(this, "next", PIN_TYPE_TUAN),
		]

		this.params.add(NodeParam("column", false))

		this.layout()

		this.deep = 0

		this.roof_height   = 0
		this.middle_height = 0
		this.column_height = 0

		this.tot_height = 0

		this.max_deep = 0
	}

	resolve_deep(bf)
	{
		var column = this.query_param("column").value
		if (column) {
			bf.column_idx.add(0)
		}

		this.max_deep = TuanHelper.calc_tree_deep(this)
	}

	resolve_height(bf)
	{
		this.column_height = TuanHelper.calc_column_layer_height(bf, this)
		this.middle_height = TuanHelper.calc_middle_layer_height(bf, this)
		this.roof_height   = (this.max_deep * 2 * BEAM_DX + 0.3 * 2) / 3

		this.tot_height = this.column_height + this.middle_height + this.roof_height
	}

	resolve_geo(bf)
	{
		var pos = Vector3(0, this.tot_height, 0)
		var ret = [ CompNodes.create_tuan(pos) ]
//		var ret = []

		var next = Blueprint.get_output_node(this, "next")
		if (!next) {
			return Variant(ret)
		}

		var btm = next.tot_height + next.calc_beam_y_offset(bf) + next.l_beam_h

		var dh = this.tot_height - btm

		var column = this.query_param("column").value
		if (column) {
			ret.add(this.create_column(bf, pos, dh))
		}

		var v_gong = nil
		if (CONTEXT.vert_conn and CONTEXT.gen_panjian) {
			v_gong = GongHelper.load_dou_gong("../yingzao/11_dg_song/802_pan_jian_2d.ves")
		} else {
			v_gong = GongHelper.load_dou_gong("../yingzao/11_dg_song/806_ding_hua_ji_tuan.ves")
		}

		if (v_gong) 
		{
			var h_gong = 0.06 + 0.21 + 0.21 + (0.12 - 0.02)
			v_gong = TransNodes.transform(VariantProxy(v_gong), Vector3(pos.x, pos.y - h_gong, pos.z), -Math.pi() * 0.5)

			if (dh > h_gong)
			{
				var h_column = dh - h_gong
				var v_column = this.create_zhuru_column(Vector3(pos.x, pos.y - dh + h_column, pos.z), h_column)
				ret.add(v_column)
			}
			
			ret.add(v_gong)
		}

		if (CONTEXT.hori_conn)
		{
			var off_h = 0.2
			var c = CompNodes.create_cha_shou(Vector3(pos.x, pos.y - off_h, pos.z), dh - off_h, BEAM_DX - 0.35)
			ret.add(c)
		}

		return Variant(ret)
	}

	resolve_pj_geo(bf, z)
	{
		var pos = Vector3(0, this.tot_height, 0)
		var h_gong = 0.06 + 0.21 + 0.21 + (0.12 - 0.02)

		var fang0 = CompNodes.create_pj_fang(Vector3(pos.x, pos.y - h_gong + 0.06, pos.z + z + FRAME_DIST * 0.5))
		var fang1 = CompNodes.create_pj_fang(Vector3(pos.x, pos.y - h_gong + 0.06 + 0.21, pos.z + z - FRAME_DIST * 0.5))

		return Variant([ fang0, fang1 ])
	}

	get_title()
	{
		return "脊槫"
	}

	create_column(bf, pos, dh)
	{
		var h_beam = 0.21
		var dou_h = 0.12

		var p = Vector3(pos.x, pos.y - dh - h_beam - dou_h, pos.z)

		var column = CompNodes.create_column(p, this.tot_height - dh - h_beam - dou_h)
		var dou = TransNodes.translate(LU_DOU, p)

		if (CONTEXT.vert_conn) {
			var e_fang = CompNodes.create_lan_e_fang(p)
			return Variant([ dou, column, e_fang ])
		} else {
			return Variant([ dou, column ])
		}
	}

	create_zhuru_column(pos, height)
	{
		return CompNodes.create_column(pos, height, 0.2, false)
	}
}