import "blueprint.pin" for Pin
import "blueprint.node" for Node
import "blueprint.blueprint" for Blueprint
import "blueprint.variant" for Variant
import "blueprint.node_param" for NodeParam
import "blueprint.variant_proxy" for VariantProxy
import "geograph.brush" for BrushInstanced
import "geograph.variant" for VAR_TYPE_GEO, VarGeo
import "gonggraph.component.dou" for Dou
import "gonggraph.component.lu_dou" for LuDou
import "gonggraph.component.inner_gong" for InnerGong
import "gonggraph.component.ti_mu" for TiMu
import "gonggraph.component.fang" for Fang
import "gonggraph.component.cheng_chuan_fang" for ChengChuanFang
import "gonggraph.component.shua_tou" for ShuaTou
import "gonggraph.component.beam" for Beam
import "gonggraph.component.hua_gong" for HuaGong
import "gonggraph.component.hori_gong" for HoriGong
import "gonggraph.component.liao_yan_fang" for LiaoYanFang
import "gonggraph.tools.hori_merge" for HoriMerge
import "gonggraph.tools.vert_merge" for VertMerge
import "gonggraph.tools_nodes" for ToolsNodes
import "gonggraph.dou_gong" for DouGong
import "beamgraph.variant" for PIN_TYPE_TUAN
import "beamgraph.constant" for BEAM_DX, BEAM_HEAD_LEN, FRAME_DIST, YUAN_INSTANCED, FRAME_STYLE, STYLE_TING_TANG, STYLE_DIAN_TANG, STYLE_CHUAN_DOU, DG_STEPS
import "beamgraph.comp_nodes" for CompNodes
import "beamgraph.context" for CONTEXT
import "beamgraph.trans_nodes" for TransNodes
import "beamgraph.gong_helper" for GongHelper
import "beamgraph.beam_helper" for BeamHelper
import "beamgraph.tuan_helper" for TuanHelper
import "maths.vector" for Vector3
import "math" for Math
import "maths" for Matrix44
import "editor.node_cache" for NODE_CACHE

class Tuan is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "prev", PIN_TYPE_TUAN),
		]
		this.exports = [
			Pin(this, "next", PIN_TYPE_TUAN),
		]

		this.params.add(NodeParam("l_column", true))
		this.params.add(NodeParam("r_column", true))

		this.layout()

		this.title = "槫"

		this.deep = 0

		this.roof_height = 0
		this.base_height = 0

		this.tot_height = 0

		this.l_beam_h = 0
		this.r_beam_h = 0
	}

	on_node_dirty()
	{
		CONTEXT.prop_changed = true
	}

	resolve_deep(bf)
	{
		this.deep = this.calc_deep()

		var l_column = this.query_param("l_column").value
		if (l_column) {
			bf.column_idx.add(-this.deep)
		} 

		var r_column = this.query_param("r_column").value
		if (r_column) {
			bf.column_idx.add(this.deep)
		}
	}

	resolve_height(bf)
	{
		import "beamgraph.nodes.sub_tuan" for SubTuan

		this.roof_height = 0
		this.base_height = 0

		var root = this.get_root_tuan()
		if (root) {
			this.base_height = root.column_height + root.middle_height
		}

		var sides = this.is_curr_sides()
		
		// calc roof height
		if (!sides) 
		{
			var deep = this.deep - root.deep

			var prev = Blueprint.get_input_node(this, "prev")
			if (prev)
			{
				var d = root.max_deep - deep
				var dh = 0.1 * Math.pow(0.5, (deep - 1)) * root.roof_height
				this.roof_height = d / (d + 1) * prev.roof_height - dh
			}
			else
			{
				this.roof_height = (root.max_deep - deep) / root.max_deep * root.roof_height
			}
		}

		this.tot_height = this.base_height + this.roof_height

		// calc beam height
		if (root is SubTuan) 
		{
			this.l_beam_h = BeamHelper.calc_beam_height(2)
			this.r_beam_h = this.l_beam_h
		}
		else
		{
			var deep = this.deep
			if (sides and FRAME_STYLE == STYLE_DIAN_TANG) {
				deep = deep - 1
			}
			this.l_beam_h = BeamHelper.calc_beam_height(this.calc_left_beam_dist(bf, -deep))
			this.r_beam_h = BeamHelper.calc_beam_height(this.calc_right_beam_dist(bf, deep))
		} 
	}

	resolve_geo(bf)
	{
		var ret = []

		this.title = this.calc_title(bf)

		var rot = -Math.pi() * 0.5
		var pos_l = Vector3(-BEAM_DX * this.deep, this.tot_height, 0)
		var pos_r = Vector3( BEAM_DX * this.deep, this.tot_height, 0)

		var root = this.get_root_tuan()
		var sides = this.is_curr_sides()
		import "beamgraph.nodes.ji_tuan" for JiTuan
		if ((FRAME_STYLE == STYLE_DIAN_TANG and root is JiTuan) or !sides) {
			ret.add(CompNodes.create_tuan(pos_l))
			ret.add(CompNodes.create_tuan(pos_r))
		}

		var l_column = this.query_param("l_column").value
		if (l_column) {
			ret.add(this.create_column(bf, pos_l))
		} else {
			ret.add(this.create_tuo_feng(bf, pos_l))
		}
		if (sides) {
			ret.add(this.create_dou_gong(bf, pos_l, false))
		} else {
			ret.add(this.create_3s_dg(pos_l, l_column))
			if (CONTEXT.hori_conn) {
				ret.add(this.create_tuo_jiao(bf, pos_l, false))			
			}
		}
		var dist_l = this.calc_left_beam_dist(bf, -this.deep)
		ret.add(this.create_beam(dist_l, pos_l, true))

		var r_column = this.query_param("r_column").value
		if (r_column) {
			ret.add(this.create_column(bf, pos_r))
		} else {
			ret.add(this.create_tuo_feng(bf, pos_r))
		}

		if (sides) {
			ret.add(this.create_dou_gong(bf, pos_r, true))
		} else {
			ret.add(this.create_3s_dg(pos_r, r_column))
			if (CONTEXT.hori_conn) {
				ret.add(this.create_tuo_jiao(bf, pos_r, true))			
			}
		}
		var dist_r = this.calc_right_beam_dist(bf, this.deep)
		if (dist_r != this.deep * 2) {
			ret.add(this.create_beam(dist_r, pos_r, false))			
		}

		if (CONTEXT.yuan) {
			ret.add(this.create_yuan(bf))
		}

		if (!bf.shun_fu_chuan) {
			var chuan = this.create_shun_fu_chuan(bf)
			if (chuan) {
				ret.add(chuan)
				bf.shun_fu_chuan = true
			}
		}

		if (sides and FRAME_STYLE == STYLE_DIAN_TANG) 
		{
			if (root is JiTuan)
			{
				var y = pos_l.y - root.middle_height
				ret.add(CompNodes.create_nei_e_fang(Vector3(0, y, 0), 0.3, BEAM_DX * this.deep * 2))

				ret.add(this.create_ping(bf))
			}
			else
			{
				var h = 0.27
				var y = pos_l.y + this.calc_beam_y_offset() + 0.21 * 2
				ret.add(CompNodes.create_nei_e_fang(Vector3(0, y, 0), h, BEAM_DX * (this.deep - 2) * 2))

				var x = BEAM_DX * (this.deep - 2)
				ret.add(CompNodes.create_cheng_yuan_fang(Vector3(-x, y, 0)))
				ret.add(CompNodes.create_cheng_yuan_fang(Vector3( x, y, 0)))

				if (bf.cao == "center") {
					ret.add(CompNodes.create_cheng_yuan_fang(Vector3(0, y, 0)))
				} else if (bf.cao == "single") {
					ret.add(CompNodes.create_cheng_yuan_fang(Vector3(-x / 3, y, 0)))
				} else if (bf.cao == "double") {
					ret.add(CompNodes.create_cheng_yuan_fang(Vector3(-x / 3, y, 0)))
					ret.add(CompNodes.create_cheng_yuan_fang(Vector3( x / 3, y, 0)))					
				}

				// ping
				var sz_z = 2.0
				var step_len = 0.05
				var l_sub_ping = CompNodes.create_ping(Vector3(- BEAM_DX * this.deep + step_len, y, 0), BEAM_DX * 2 - step_len * 2, sz_z)
				var r_sub_ping = TransNodes.rotate(VariantProxy(l_sub_ping), Math.pi())
				ret.add(l_sub_ping)
				ret.add(r_sub_ping)
			}
		}

		return Variant(ret)
	}

	resolve_pj_geo(bf, z)
	{
		var sides = this.is_curr_sides()
		if (sides) {
			return nil
		}

		var pos_l = Vector3(-BEAM_DX * this.deep, this.tot_height, 0)
		var pos_r = Vector3( BEAM_DX * this.deep, this.tot_height, 0)

		var l_column = this.query_param("l_column").value
		var r_column = this.query_param("r_column").value

		var dz = 0
		if (Math.mod(this.deep, 2) > 0) {
			dz = FRAME_DIST * 0.5
		} else {
			dz = -FRAME_DIST * 0.5
		}

		var dy = -(0.12 - 0.02)

		var fang_l = CompNodes.create_pj_fang(Vector3(pos_l.x, pos_l.y + dy, pos_l.z + z + dz))
		var fang_r = CompNodes.create_pj_fang(Vector3(pos_r.x, pos_r.y + dy, pos_r.z + z - dz))

		return Variant([ fang_l, fang_r ])
	}

	get_title()
	{
		return this.title	
	}

	calc_title(bf)
	{
		if (this.deep == 1) {
			return "上平槫"
		} else if (this.is_curr_sides()) {
			return "承椽槫"
		} else if (this.is_next_sides()) {
			return "下平槫"
		} else {
			return "中平槫"
		}
	}

	calc_deep()
	{
		var prev = Blueprint.get_input_node(this, "prev")
		if (prev) {
			return prev.deep + 1
		} else {
			return 0
		}
	}

	calc_beam_y_offset()
	{
		var dy = 0
		if (this.is_curr_sides()) 
		{
			var root = this.get_root_tuan()
			dy = - root.middle_height + 0.12 + 0.21

			// double beam
			import "beamgraph.nodes.ji_tuan" for JiTuan
			if (FRAME_STYLE == STYLE_DIAN_TANG and root is JiTuan) 
			{
				var layers = 0

				var steps = TuanHelper.calc_steps(this.get_root_tuan())
				if (steps == 5 or steps == 6) {
					layers = 4
				} else {
					layers = steps - 2
				}

				dy = dy + 0.21 * layers - 0.06
			}
		} 
		else 
		{
			dy = - (0.21 + 0.12 - 0.02)
		}
		return dy
	}

	create_column(bf, pos)
	{
		var h_gong = 0

		var sides = this.is_curr_sides()
		if (FRAME_STYLE == STYLE_TING_TANG)
		{
			if (sides) {
				h_gong = this.get_root_tuan().middle_height
			} else {
				h_gong = this.calc_3s_gong_height(true)
			}
		}
		else if (FRAME_STYLE == STYLE_DIAN_TANG)
		{
			h_gong = this.get_root_tuan().middle_height
		}

		var p = Vector3(pos.x, pos.y - h_gong, pos.z)

		var column = CompNodes.create_column(p, this.tot_height - h_gong)
		if (CONTEXT.vert_conn) {
			var e_fang = CompNodes.create_lan_e_fang(p)
			return Variant([ column, e_fang ])
		} else {
			return column
		}
	}

	calc_left_beam_dist(bf, idx)
	{
		var dist = -idx * 2
		for (var i in bf.column_idx)
		{
			if (i > idx) {
				var d = i - idx
				if (d < dist) {
					dist = d
				}
			}
		}
		return dist
	}

	calc_right_beam_dist(bf, idx)
	{
		var dist = idx * 2
		for (var i in bf.column_idx)
		{
			if (i < idx) {
				var d = idx - i
				if (d < dist) {
					dist = d
				}
			}
		}
		return dist
	}

	create_beam(dist, pos, left)
	{
		var h = 0
		if (left) {
			h = this.l_beam_h
		} else {
			h = this.r_beam_h
		}

		var y = pos.y + this.calc_beam_y_offset()

		var len = dist * BEAM_DX + BEAM_HEAD_LEN * 2
		if (dist == dist * 2) {
			len = len + BEAM_HEAD_LEN
		}

		var x = pos.x
		if (left) {
			x = x - BEAM_HEAD_LEN
		} else {
			x = x - len + BEAM_HEAD_LEN
		}

		return CompNodes.create_straight_beam(Vector3(x, y, pos.z), h, len)		
	}

	create_tuo_feng(bf, pos)
	{
		var next = Blueprint.get_output_node(this, "next")
		if (!next) {
			return nil
		}

		var top = this.tot_height + this.calc_beam_y_offset() - 0.06

		var btm = next.tot_height + next.calc_beam_y_offset()
		if (pos.x < 0) {
			btm = btm + next.l_beam_h
		} else {
			btm = btm + next.r_beam_h		
		}

		if (top <= btm) {
			return nil
		}

		if (FRAME_STYLE == STYLE_TING_TANG)	
		{
			return CompNodes.create_tuo_feng(Vector3(pos.x, btm, pos.z), top - btm)
		} 
		else if (FRAME_STYLE == STYLE_DIAN_TANG) 
		{
			var w = 0.15
			var h = top - btm
			var l = 3
			var h1 = 0.22
			if (h < h1) 
			{
				return CompNodes.create_box(Vector3(pos.x - h * l * 0.5, btm, pos.z), Vector3(h * l, h, w))
			}
			else
			{
				var t1 = CompNodes.create_box(Vector3(pos.x - h1 * l * 0.5, btm, pos.z), Vector3(h1 * l, h1, w))

				var h2 = h - h1
				var t2 = CompNodes.create_box(Vector3(pos.x - h2 * l * 0.5, btm + h1, pos.z), Vector3(h2 * l, h2, w - 0.02))

				return Variant([ t1, t2 ])
			}
		} 
		else 
		{
			return nil
		}
	}

	calc_3s_gong_height(column)
	{
		var h_dou = 0
		if (column) {
			h_dou = 0.12
		} else {
			h_dou = 0.06
		}
		return h_dou + 0.21 + (0.12 - 0.02)
	}

	create_dou_gong(bf, pos, rot)
	{
		if (FRAME_STYLE == STYLE_TING_TANG) 
		{
			return this.create_4s_dg(pos, rot)
		} 
		else if (FRAME_STYLE == STYLE_DIAN_TANG) 
		{
//			var upper = this.create_3s_dg(pos, true)
			
			var ti_y = pos.y - (0.12 - 0.02)
			var upper = CompNodes.create_ti_mu(Vector3(pos.x, ti_y, pos.z))

			var lower = this.create_dian_dg(bf, pos, rot)
			return Variant([ upper, lower ])
		} 
		else 
		{
			return nil
		}
	}

	create_dian_dg(bf, pos, rot)
	{		
		import "beamgraph.nodes.sub_tuan" for SubTuan
		var sub = this.get_root_tuan() is SubTuan

		// set beam len
		var beam_len = 0
		var BEAM = NODE_CACHE.fetch("../yingzao/00_dougong2/45_straight_beam.ves", nil)
		if (sub) {
			beam_len = BEAM_DX * 2 + 0.5
		} else {
			beam_len = 0.2
		}
		Blueprint.connect(VariantProxy(Variant(beam_len)), "out", BEAM, "length")

		var v_gong = GongHelper.create_outer_column(sub)

		var y = pos.y - this.get_root_tuan().middle_height

		var angle = -Math.pi() * 0.5
		if (rot) {
			angle = angle + Math.pi()
		}

		v_gong = TransNodes.transform(VariantProxy(v_gong), Vector3(pos.x, y, pos.z), angle)

		return v_gong
	}

	create_3s_dg(pos, column)
	{
		var dou = nil
		if (column) {
			dou = LuDou()
		} else {
			dou = Dou()
		}

		var nodes = [ dou ]

		var gong = nil
		if (CONTEXT.vert_conn and CONTEXT.gen_panjian)
		{
			var g = InnerGong()
			var f = Fang()
			gong = HoriMerge()

			if (Math.mod(this.deep, 2) > 0) {
				Blueprint.connect(g, "next", gong, "left")
				Blueprint.connect(f, "next", gong, "right")
			} else {
				Blueprint.connect(f, "next", gong, "left")
				Blueprint.connect(g, "next", gong, "right")
			}

			nodes.add(g)
			nodes.add(f)
		}
		else
		{
			gong  = InnerGong()
		}
		nodes.add(gong)

		var ti = TiMu()
		nodes.add(ti)

		Blueprint.connect(dou, "hori", gong, "prev")
		Blueprint.connect(gong, "next", ti, "prev")

		var geos = []

		var dg = DouGong()
		dg.rebuild(nodes, geos)

		var v_gong = Variant(geos)

		var h_gong = this.calc_3s_gong_height(column)
		v_gong = TransNodes.transform(VariantProxy(v_gong), Vector3(pos.x, pos.y - h_gong, pos.z), -Math.pi() * 0.5)

		return v_gong
	}

	create_4s_dg(pos, rot)
	{
		var lu = LuDou()

		var ni_dao = InnerGong()
		var bi_man = InnerGong()
		var zhu_fang = Fang()
		var cheng_fang = ChengChuanFang()

		var hua = HuaGong()
		var shua = ShuaTou()		
		var ling = HoriGong()
		var yan = LiaoYanFang()

		Blueprint.connect(lu, "hori", ni_dao, "prev")
		Blueprint.connect(ni_dao, "next", bi_man, "prev")
		Blueprint.connect(bi_man, "next", zhu_fang, "prev")
		Blueprint.connect(zhu_fang, "next", cheng_fang, "prev")

		Blueprint.connect(lu, "vert", hua, "prev")
		Blueprint.connect(hua, "next", shua, "prev")

		Blueprint.connect(hua, "outer", ling, "prev")
		Blueprint.connect(ling, "next", yan, "prev")

		var geos = []

		var dg = DouGong()
		dg.rebuild([ lu, ni_dao, bi_man, zhu_fang, cheng_fang, shua, hua, ling, yan ], geos)

		var v_gong = Variant(geos)

		var h_gong = this.get_root_tuan().middle_height

		var angle = -Math.pi() * 0.5
		if (rot) {
			angle = angle + Math.pi()
		}

		v_gong = TransNodes.transform(VariantProxy(v_gong), Vector3(pos.x, pos.y - h_gong, pos.z), angle)

		return v_gong
	}

	create_tuo_jiao(bf, pos, rot)
	{
		var next = Blueprint.get_output_node(this, "next")
		if (!next) {
			return nil
		}

		var top = pos.y - 0.05

		var btm = next.tot_height + next.calc_beam_y_offset()
		if (pos.x < 0) {
			btm = btm + next.l_beam_h
		} else {
			btm = btm + next.r_beam_h		
		}

		var dx = 0.3
		if (FRAME_STYLE == STYLE_DIAN_TANG) {
			dx = 0.5
		} 

		return CompNodes.create_tuo_jiao(Vector3(pos.x, top, pos.z), top - btm, BEAM_DX - dx, rot)
	}

	create_yuan(bf)
	{
		var prev = Blueprint.get_input_node(this, "prev")
		if (!prev) {
			return nil
		}

		var d = 0.08 * 2

		var l_p0 = Vector3(-BEAM_DX * this.deep, this.tot_height + d, 0)
		var l_p1 = Vector3(-BEAM_DX * prev.deep, prev.tot_height + d, 0)

		var ex_len = 0
		var ex_len_f = 0
		if (this.is_curr_sides()) 
		{
			var angle = Math.atan2(l_p1.y - l_p0.y, l_p1.x - l_p0.x)
			ex_len = (0.3 + 0.8) / Math.cos(angle)
			ex_len_f = (0.3 + 0.8 + 0.48) / Math.cos(angle)
		}

		var l_yuan = CompNodes.create_yuan(l_p0, l_p1, ex_len)

		// fei zi
		if (this.is_curr_sides()) 
		{
			var fei = CompNodes.create_fei_zi(l_p0, Vector3(l_p1.x, l_p1.y, l_p1.z), ex_len_f)
			l_yuan = Variant([ l_yuan, fei ])
		}

		if (YUAN_INSTANCED)
		{
			l_yuan = ToolsNodes.brush_merge(l_yuan)

			var instanced = BrushInstanced()
			instanced.brush = l_yuan.value.shape

			var num = 17
			var dist = 0.16
			for (var i in 0..num) 
			{
				var l_mat = Matrix44.init()
				l_mat.translate(0, 0, i * dist - (num - 1) * 0.5 * dist)
				instanced.transforms.add(l_mat)

				var r_mat = Matrix44.init()
				r_mat.rotate(0, Math.pi(), 0)
				r_mat.translate(0, 0, i * dist - (num - 1) * 0.5 * dist)
				instanced.transforms.add(r_mat)
			}

			var geo = VarGeo()
			geo.shape = instanced

			return Variant(VAR_TYPE_GEO, geo)
		}
		else
		{
			var r_yuan = TransNodes.rotate(VariantProxy(l_yuan), Math.pi())

			var ret = []

			var num = 17
			var dist = 0.16
			for (var i in 0..num) {
				ret.add(TransNodes.translate(VariantProxy(Variant([l_yuan, r_yuan])), Vector3(0, 0, i * dist - (num - 1) * 0.5 * dist)))
			}

			return Variant(ret)
		}
	}

	create_shun_fu_chuan(bf)
	{
		var l_column = this.query_param("l_column").value
		var r_column = this.query_param("r_column").value
		if (!l_column or !r_column) {
			return nil
		}

		var next_cc = this.find_next_cc()
		if (!next_cc or next_cc.deep - this.deep <= 1) {
			return nil
		}

		var len = BEAM_DX * this.deep * 2
		var next_beam_h = BeamHelper.calc_beam_height(next_cc.calc_left_beam_dist(bf, -next_cc.deep))
		if (this.is_tuan_sides(next_cc)) {
			next_beam_h = next_beam_h + 0.21
		}
		return CompNodes.create_shun_fu_chuan(Vector3(-len * 0.5, next_cc.tot_height - next_beam_h, 0), len)
	}

	create_inner_ping_border(ret, x)
	{
		var sz_z = 2.0

		var y = this.tot_height + this.calc_beam_y_offset() - 0.21

		if (DG_STEPS < 7) 
		{
			ret.add(CompNodes.create_ping_border(Vector3(x, y - 0.21, 0), sz_z, false))
			ret.add(CompNodes.create_ping_border(Vector3(x, y - 0.21, 0), sz_z, true))
		}
		else if (DG_STEPS == 7)
		{
			ret.add(CompNodes.create_ping_border(Vector3(x, y - 0.21 * 2, 0), sz_z, false))
			ret.add(CompNodes.create_ping_border(Vector3(x, y - 0.21 * 2, 0), sz_z, true))
			ret.add(CompNodes.create_ping_border(Vector3(x + 0.28, y - 0.21, 0), sz_z, false))
			ret.add(CompNodes.create_ping_border(Vector3(x - 0.28, y - 0.21, 0), sz_z, true))			
		}
		else if (DG_STEPS == 8)
		{
			ret.add(CompNodes.create_ping_border(Vector3(x, y - 0.21 * 3, 0), sz_z, false))
			ret.add(CompNodes.create_ping_border(Vector3(x, y - 0.21 * 3, 0), sz_z, true))
			ret.add(CompNodes.create_ping_border(Vector3(x + 0.28, y - 0.21 * 2, 0), sz_z, false))
			ret.add(CompNodes.create_ping_border(Vector3(x - 0.28, y - 0.21 * 2, 0), sz_z, true))				
			ret.add(CompNodes.create_ping_border(Vector3(x + 0.28 + 0.25, y - 0.21, 0), sz_z, false))
			ret.add(CompNodes.create_ping_border(Vector3(x - 0.28 - 0.25, y - 0.21, 0), sz_z, true))			
		}
	}

	create_ping(bf)
	{
		var ret = []

		var x = -BEAM_DX * this.deep
		var y = this.tot_height + this.calc_beam_y_offset() - 0.21

		var sz_x = BEAM_DX * this.deep * 2
		var sz_z = 2.0

		var step_len = 0.3
		if (DG_STEPS == 7) {
			step_len = step_len + 0.25
		} else if (DG_STEPS == 8) {
			step_len = step_len + 0.5
		}

		if (!bf.cao)
		{
			ret.add(CompNodes.create_ping(Vector3(x + step_len, y, 0), sz_x - step_len * 2, sz_z))
		}
		else if (bf.cao == "center")
		{
			var cx = x + sz_x * 0.5

			ret.add(CompNodes.create_ping(Vector3( x + step_len, y, 0), sz_x * 0.5 - step_len * 2, sz_z))
			ret.add(CompNodes.create_ping(Vector3(cx + step_len, y, 0), sz_x * 0.5 - step_len * 2, sz_z))

			this.create_inner_ping_border(ret, cx)
		}
		else if (bf.cao == "single")
		{
			var cx = x + sz_x * 0.25

			ret.add(CompNodes.create_ping(Vector3( x + step_len, y, 0), sz_x * 0.25 - step_len * 2, sz_z))
			ret.add(CompNodes.create_ping(Vector3(cx + step_len, y, 0), sz_x * 0.75 - step_len * 2, sz_z))

			this.create_inner_ping_border(ret, cx)
		}
		else if (bf.cao == "double")
		{
			var cx_0 = x + sz_x * 0.25
			var cx_1 = x + sz_x * 0.75

			ret.add(CompNodes.create_ping(Vector3(   x + step_len, y, 0), sz_x * 0.25 - step_len * 2, sz_z))
			ret.add(CompNodes.create_ping(Vector3(cx_0 + step_len, y, 0), sz_x * 0.5  - step_len * 2, sz_z))
			ret.add(CompNodes.create_ping(Vector3(cx_1 + step_len, y, 0), sz_x * 0.25 - step_len * 2, sz_z))

			this.create_inner_ping_border(ret, cx_0)
			this.create_inner_ping_border(ret, cx_1)
		}

		var l_borders = []
		if (DG_STEPS == 5)
		{
			l_borders.add(CompNodes.create_ping_border(Vector3(x, y - 0.21, 0), sz_z, true))
			l_borders.add(CompNodes.create_ping_border(Vector3(x, y - 0.21, 0), sz_z, false))
			l_borders.add(CompNodes.create_box(Vector3(x - 0.5, y, 0), Vector3(0.2, 0.02, sz_z)))
		}
		else if (DG_STEPS == 6)
		{
			l_borders.add(CompNodes.create_ping_border(Vector3(x, y - 0.21, 0), sz_z, true))
			l_borders.add(CompNodes.create_ping_border(Vector3(x - 0.3, y, 0), sz_z, true))			
			l_borders.add(CompNodes.create_ping_border(Vector3(x, y - 0.21, 0), sz_z, false))
			l_borders.add(CompNodes.create_box(Vector3(x - 0.75, y + 0.21, 0), Vector3(0.2, 0.02, sz_z)))
		}
		else if (DG_STEPS == 7)
		{
			for (var i = 0; i < 2; i = i + 1) 
			{
				var dx = 0.3 * i
				var dy = -0.21 * (2 - i)
				l_borders.add(CompNodes.create_ping_border(Vector3(x - dx, y + dy, 0), sz_z, true))
				l_borders.add(CompNodes.create_ping_border(Vector3(x + dx, y + dy, 0), sz_z, false))
			}
			l_borders.add(CompNodes.create_ping_border(Vector3(x - 0.6, y, 0), sz_z, true))			
			l_borders.add(CompNodes.create_box(Vector3(x - 1, y + 0.21, 0), Vector3(0.2, 0.02, sz_z)))
		}
		else if (DG_STEPS == 8)
		{
			for (var i = 0; i < 3; i = i + 1) 
			{
				var dx = 0.3 * i
				var dy = -0.21 * (3 - i)
				l_borders.add(CompNodes.create_ping_border(Vector3(x - dx, y + dy, 0), sz_z, true))
				l_borders.add(CompNodes.create_ping_border(Vector3(x + dx, y + dy, 0), sz_z, false))
			}
			l_borders.add(CompNodes.create_box(Vector3(x - 1.5, y, 0), Vector3(0.6, 0.02, sz_z)))
		}
		ret.add(Variant(l_borders))
		ret.add(TransNodes.rotate(VariantProxy(Variant(l_borders)), Math.pi()))

		return Variant(ret)
	}

	find_next_cc()
	{
		import "beamgraph.nodes.sub_tuan" for SubTuan

		var curr = this
		while (curr) 
		{
			var next = Blueprint.get_output_node(curr, "next")
			if (!next or next is SubTuan) {
				return nil
			}

			var l_column = next.query_param("l_column").value
			var r_column = next.query_param("r_column").value
			if (l_column and r_column) {
				return next
			}

			curr = next
		}
		return nil
	}

	is_tuan_sides(tuan)
	{
		import "beamgraph.nodes.sub_tuan" for SubTuan

		var next = Blueprint.get_output_node(tuan, "next")
		return !next or next is SubTuan
	}

	is_curr_sides()
	{
		return this.is_tuan_sides(this)
	}

	is_next_sides()
	{
		var next = Blueprint.get_output_node(this, "next")
		if (next) {
			return this.is_tuan_sides(next)
		} else {
			return false
		}
	}

	get_root_tuan()
	{
		import "beamgraph.nodes.ji_tuan" for JiTuan
		import "beamgraph.nodes.sub_tuan" for SubTuan

		var prev = Blueprint.get_input_node(this, "prev")
		while (prev) {
			if (prev is JiTuan or prev is SubTuan) {
				return prev
			}
			prev = Blueprint.get_input_node(prev, "prev")
		}

		return nil
	}	
}