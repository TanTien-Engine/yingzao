import "blueprint.blueprint" for Blueprint
import "beamgraph.constant" for FRAME_STYLE, STYLE_DIAN_TANG, STYLE_TING_TANG, DG_STEPS
import "beamgraph.gong_helper" for GongHelper
import "beamgraph.beam_helper" for BeamHelper

class TuanHelper
{
	static calc_tree_deep(root)
	{
		import "beamgraph.nodes.tuan" for Tuan

		var max_deep = 0

		var next = Blueprint.get_output_node(root, "next")
		while (next and next is Tuan) {
			max_deep = max_deep + 1
			next = Blueprint.get_output_node(next, "next")
		}

		return max_deep
	}

	static calc_steps(root)
	{
		var steps = DG_STEPS
		if (FRAME_STYLE == STYLE_DIAN_TANG) 
		{
			import "beamgraph.nodes.sub_tuan" for SubTuan
			if (root is SubTuan) {
				steps = steps - 1
			}
		}
		else if (FRAME_STYLE == STYLE_TING_TANG) 
		{
			steps = 4
		}
		return steps
	}

	static calc_middle_layer_height(root)
	{
		var steps = this.calc_steps(root)
		if (steps == 5) {
			steps = steps + 1
		}
	
		var dh = 0
		if (FRAME_STYLE == STYLE_DIAN_TANG) 
		{
			import "beamgraph.nodes.sub_tuan" for SubTuan
			if (root is SubTuan and DG_STEPS == 5) {
				dh = 0.15
			} else {
				dh = 0.3			
			}
		} 
		else if (FRAME_STYLE == STYLE_TING_TANG) 
		{
			dh = 0.15
		}

		return GongHelper.calc_dou_gong_height(steps) + dh
	}

	static calc_column_layer_height(root)
	{
		import "beamgraph.nodes.sub_tuan" for SubTuan

		var h_column = 2.5
		if (FRAME_STYLE == STYLE_DIAN_TANG) {
			h_column = 3.2
		}

		var sub_dh = 2.50
		if (FRAME_STYLE == STYLE_TING_TANG) {
			sub_dh = sub_dh + 0.4
		} else if (FRAME_STYLE == STYLE_DIAN_TANG) {
			sub_dh = sub_dh + 0.5
		}

		var next = Blueprint.get_output_node(root, "next")
		while (next) {
			if (next is SubTuan) {
				h_column = h_column + sub_dh
			}
			next = Blueprint.get_output_node(next, "next")
		}

		return h_column
	}
}